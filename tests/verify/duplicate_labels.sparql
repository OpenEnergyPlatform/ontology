# Adapted from: 
# https://github.com/CommonCoreOntology/CommonCoreOntologies/blob/v2.0-2024-11-06/.github/deployment/sparql/duplicate_label.sparql
# No two ontology elements shall have the same rdfs:label.

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT ?element ?element2 ?label ?error
WHERE {
  ?element rdfs:label ?label .
  ?element2 rdfs:label ?label .
  FILTER (?element != ?element2)
  FILTER (!isBlank(?element))
  FILTER (!isBlank(?element2))
  FILTER (regex(str(?element),"https://openenergyplatform.org/ontology/oeo/"))
  FILTER (regex(str(?element2),"https://openenergyplatform.org/ontology/oeo/"))
  BIND (concat("WARNING: The following ontology elements have the same rdfs:label ", str(?element), " and ", str(?element2)) AS ?error)
}
ORDER BY DESC(UCASE(str(?label)))